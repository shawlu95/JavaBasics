// relative to project directory (where build.gradle lives)
ext.versionFile = file('version.properties')

// task load version is done first
// no need for ledt shift operator
// Task configuration blocks are always executed before task actions
task loadVersion {
    project.version = readVersion()
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream -> versionProps.load(stream) }

    // do not need `return`
    new ProjectVersion(
      versionProps.major.toInteger(),
      versionProps.minor.toInteger(),
      versionProps.release.toBoolean())
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    @Override
    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}

class ReleaseVersionListener implements TaskExecutionGraphListener {
  final static String releaseTaskPath = ':release'
  @Override
  void graphPopulated(TaskExecutionGraph taskGraph) {
    if(taskGraph.hasTask(releaseTaskPath)) {
      // find release task
      List<Task> allTasks = taskGraph.allTasks
      Task releaseTask = allTasks.find {it.path == releaseTaskPath }

      // every task knows its project
      Project project = releaseTask.project

      if(!project.version.release) {
        project.version.release = true
        project.ant.propertyfile(file: project.versionFile) {
          entry(key: 'release', type: 'string', operation: '=', value: 'true')
        }
      }
    }
  }
}
def releaseVersionListener = new ReleaseVersionListener()
gradle.taskGraph.addTaskExecutionGraphListener(releaseVersionListener)

task release {
  println "go"
}
